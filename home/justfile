set dotenv-load := false
set export := true
set quiet := true
set unstable := true

export JUST_UNSTABLE := "true"

mod claude '.justfiles/claude.justfile'
mod codex '.justfiles/codex.justfile'
mod copilot '.justfiles/copilot.justfile'
mod gemini '.justfiles/gemini.justfile'
mod mise '.justfiles/mise.justfile'
mod ralphtui '.justfiles/ralphtui.justfile'

HOST := `hostname -s`

[private]
@default:
    just --list --list-submodules

# Setup mise
[group("setup")]
@bootstrap:
    just mise::bootstrap

# Format and overwrite justfile
[group("utils")]
@fmt:
    just --fmt --unstable
    for f in .justfiles/*.justfile; do just --fmt --unstable -f "$f"; done

# Upgrade all tools (uv and mise, and all justfiles)
[group("maintenance")]
@upgrade:
    uv tool upgrade --all
    just mise::upgrade
    for f in .justfiles/*.justfile; do echo "\033[1;36m>>> Running upgrade for $f\033[0m"; just -f "$f" upgrade || true; done
